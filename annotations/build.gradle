plugins {
    id 'java'
    id 'maven'
    id 'signing'
}

sourceCompatibility = '1.8'

group = "com.github.h908714124"
archivesBaseName = "auto-builder-annotations"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.0-RC1'
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name': 'auto.builder',
                'Implementation-Version': project.properties['version']
        )
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

// https://docs.gradle.org/current/userguide/signing_plugin.html
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        allprojects {
            ext."signing.keyId" = System.getenv( 'SIGNING_KEY_ID' )
            ext."signing.password" = System.getenv( 'SIGNING_PASSWORD' )
        }
    }
}

// https://central.sonatype.org/pages/gradle.html
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getenv( 'OSS_USER' ), password: System.getenv( 'OSS_PASS' ))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.getenv( 'OSS_USER' ), password: System.getenv( 'OSS_PASS' ))
            }

            pom.project {
                name 'auto-builder-annotations'
                packaging 'jar'
                artifactId 'auto-builder-annotations'
                description 'Convenience builders for auto-value'
                url 'https://github.com/h908714124/auto-builder'

                scm {
                    connection 'scm:svn:https://github.com/h908714124/auto-builder.git'
                    developerConnection 'scm:svn:https://github.com/h908714124/auto-builder.git'
                    url 'https://github.com/h908714124/auto-builder'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'h908714124'
                        name 'h908714124'
                        email 'kraftdurchblumen@gmx.de'
                    }
                }
            }
        }
    }
}

